// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O.
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory
 * location specified by address. If load==1, the in value is loaded
 * into the memory location specified by address. This value becomes
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used.
 * Access to address>0x6000 is invalid. Access to any address in
 * the range 0x4000-0x5FFF results in accessing the screen memory
 * map. Access to address 0x6000 results in accessing the keyboard
 * memory map. The behavior in these addresses is described in the
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:

    //Ram first 16k, Screen next 8k, Keyboard is last word, above that invalid
    RAM16K(in=in, load=loadRam, address=address[0..13], out=ramOut);
    Screen(in=in, load=loadScreen, address=address[0..12], out=screenOut);
    Keyboard(out=kbOut);

    //Memory address ranges
    //                14  13  12
    //Max RAM address 0   1   1   1111  1111  1111
    //Max VID address 1   0   1   1111  1111  1111
    //KBD address     1   1   0   0000  0000  0000

    //Handle loading (keyboard is read-only)
    DMux(in=load, a=loadRam, b=loadIo, sel=address[14]);
    DMux(in=loadIo, a=loadScreen, b=loadKb, sel=address[13]);

    //Handle output
    Mux16(a=screenOut, b=kbOut, sel=address[13], out=ioOut);
    Mux16(a=ramOut, b=ioOut, sel=address[14], out=out);

}
